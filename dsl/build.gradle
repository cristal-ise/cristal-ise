apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'project-report'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.6.1"
    }
}

apply plugin: 'io.codearte.nexus-staging'

group       = 'org.cristalise'
version     = '3.4.0-SNAPSHOT'
description = 'CRISTAL-iSE Domain Specific Language module for bootstrapping and funtional testing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    cristaliseVer   = '3.4.0-SNAPSHOT'
    groovyVer       = '2.4.10'
    commonsLangVer  = '2.6'
    xmlunitVer      = '2.3.0'
    junitVer        = '4.11'
    awaitilityVer   = '2.0.0'
    spockVer        = '1.1-groovy-2.4'
    mvel2Ver        = '2.2.8.Final'
    spockReportsVer = '1.3.0'
    h2Ver           = '1.4.193'
}

apply from: 'deploy.gradle'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile group: 'org.cristalise', name: 'cristalise-kernel',            version: cristaliseVer
    compile group: 'org.cristalise', name: 'cristalise-dev',               version: cristaliseVer
    compile group: 'org.cristalise', name: 'cristalise-jooqdb',            version: cristaliseVer
    compile group: 'org.cristalise', name: 'cristalise-xpath-outcomeinit', version: '3.3.0'
    compile group: 'org.cristalise', name: 'cristalise-trigger',           version: '3.3.0'
    compile group: 'org.cristalise', name: 'cristalise-gui',               version: cristaliseVer

    compile group: 'org.codehaus.groovy', name: 'groovy-all',   version: groovyVer
    compile group: 'commons-lang',        name: 'commons-lang', version: commonsLangVer
    compile group: 'org.xmlunit',         name: 'xmlunit-core', version: xmlunitVer

    testCompile group: 'com.h2database',     name: 'h2',                version: h2Ver
    testCompile group: 'junit',              name: 'junit',             version: junitVer
    testCompile group: 'org.awaitility',     name: 'awaitility-groovy', version: awaitilityVer
    testCompile group: 'org.spockframework', name: 'spock-core',        version: spockVer
    testCompile group: 'org.mvel',           name: 'mvel2',             version: mvel2Ver
    testCompile(group: 'com.athaydes',       name: 'spock-reports',     version: spockReportsVer) { exclude(module: '*') }
}
